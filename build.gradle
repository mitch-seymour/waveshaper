buildscript {
  if (!project.hasProperty("sonatypeUsername") || !project.hasProperty("sonatypePassword")) {
    ext.sonatypeUsername = System.getenv("SONATYPE_USERNAME")
    ext.sonatypePassword = System.getenv("SONATYPE_PASSWORD")
  }
}

plugins {
  id "java-library"

  // for running the Demo app
  id "application"

  // for publishing the artifact to maven central
  id "maven-publish"

  // for signing the artifact when publishing to maven central
  id "signing"

  // https://plugins.gradle.org/plugin/com.jfrog.artifactory
  id "com.jfrog.artifactory" version "4.9.0"

  // https://plugins.gradle.org/plugin/com.github.sherter.google-java-format
  id "com.github.sherter.google-java-format" version "0.8"

  // https://plugins.gradle.org/plugin/net.ltgt.errorprone
  id 'net.ltgt.errorprone' version "0.6.1"
}

repositories {
  mavenCentral()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext {
  // https://mvnrepository.com/artifact/com.google.errorprone/error_prone_core
  errorProneCoreVersion = "2.3.2"

  // https://mvnrepository.com/artifact/com.google.errorprone/javac
  // required for Java 8 support
  errorProneJavacVersion = "9+181-r4173-1"

  // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
  junitVersion = "5.3.2"

  // https://search.maven.org/artifact/com.google.guava/guava
  guavaVersion = "27.0.1-jre"
}

dependencies {
  errorprone "com.google.errorprone:error_prone_core:${errorProneCoreVersion}"
  errorproneJavac "com.google.errorprone:javac:${errorProneJavacVersion}"

  testCompile "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

  implementation "com.google.guava:guava:${guavaVersion}"
}

tasks.withType(JavaCompile).configureEach { javaCompile ->
  javaCompile.dependsOn "googleJavaFormat"
  options.encoding = 'UTF-8'
  options.errorprone {
    disableWarningsInGeneratedCode = true
    excludedPaths = ".*/build/generated/.*"
  }
}

test {
  useJUnitPlatform()
}

mainClassName = 'io.waveshaper.Demo'

run {
  // Enable human friendly logging
  systemProperty "LOCAL", "local"
}


task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
  from javadoc
  archiveClassifier = 'javadoc'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'waveshaper'
      groupId 'io.waveshaper'
      version '0.1.2-SNAPSHOT'
      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom {
        name = 'Waveshaper'
        description = 'A library for load testing with synthetic waveforms'
        url = 'https://github.com/mitch-seymour/waveshaper'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'mitch-seymour'
            name = 'Mitch Seymour'
            email = 'mitchseymour@gmail.com'
          }
        }
        scm {
            connection = 'scm:git:git://github.com/mitch-seymour/waveshaper.git'
            developerConnection = 'scm:git:ssh://git@github.com:mitch-seymour/waveshaper.git'
            url = 'https://github.com/mitch-seymour/waveshaper'
        }
      }
    }
  }
  repositories {
    maven {
      url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
      credentials {
          username sonatypeUsername
          password sonatypePassword
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}


javadoc {
  if(JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}

